rootProject.name = 'DLect Root Project'

// Find the directories containing a "build.gradle" file in the root directory
// of the project. That is, every directory containing a "build.gradle" will
// be automatically the subproject of this project.


def recurse(String base, File parent) {
    parent.listFiles(new FileFilter() {
            public boolean accept(File file) {
                return file.isDirectory() && !file.name.startsWith(".") && !file.name.matches("^(src|build|target)")
            }
        }).each({ File dir ->
            String base2 = base + ":" + dir.name
            if(new File(dir, 'build.gradle').isFile()) {
                include base2
                def p = project(base2);
                p.name = base2.substring(1).replace(":", "-")
            } else {
                recurse(base2, dir);
            }
        })
}

recurse("", rootDir)